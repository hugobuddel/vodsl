/**
 * generated by Xtext - then hand edited by Paul Harrison.
 */
package net.ivoa.vodml.generator

import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.IGenerator
import org.eclipse.xtext.generator.IFileSystemAccess
import net.ivoa.vodml.vodsl.ModelDeclaration
import net.ivoa.vodml.vodsl.IncludeDeclaration
import net.ivoa.vodml.vodsl.AbstractElement
import net.ivoa.vodml.vodsl.PackageDeclaration
import net.ivoa.vodml.vodsl.ObjectType
import net.ivoa.vodml.vodsl.Enumeration
import net.ivoa.vodml.vodsl.DataType
import net.ivoa.vodml.vodsl.PrimitiveType
import net.ivoa.vodml.vodsl.VoDataModel
import net.ivoa.vodml.vodsl.EnumLiteral
import net.ivoa.vodml.vodsl.Attribute
import net.ivoa.vodml.vodsl.Constraint
import net.ivoa.vodml.vodsl.Multiplicity
import net.ivoa.vodml.vodsl.ReferableElement

/**
 * Generates code from your model files on save.
 * 
 * see http://www.eclipse.org/Xtext/documentation.html#TutorialCodeGeneration
 */
class VodslGenerator implements IGenerator {
	
	override void doGenerate(Resource resource, IFileSystemAccess fsa) {
		val vodecl = resource.allContents.filter(typeof(VoDataModel)).head // surely there must be a better way of getting this one....
		val modelDecl = vodecl.model
		val filename = modelDecl.name + '.vo-dml.xml'
		fsa.generateFile(filename, 
	  '''
<?xml version="1.0" encoding="UTF-8"?>
	<vo-dml:model xmlns:vo-dml="http://volute.googlecode.com/dm/vo-dml/v0.9"
              xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"
              xsi:schemaLocation="http://volute.googlecode.com/dm/vo-dml/v0.9 https://volute.googlecode.com/svn/trunk/projects/dm/vo-dml/xsd/vo-dml.xsd">
	  <!-- file generated from VODSL -->
''' +   modelDecl.vodml
    +   vodecl.includes.map[vodml].join('')
    +   vodecl.elements.map[vodml].join('')	
	 +	'</vo-dml:model>')
	}
	
	
	def vodml (ModelDeclaration e) '''
    <vodml-id>«e.name»</vodml-id>
    <name>«e.name»</name>
    <version>«e.version»</version>
    <description>«e.description»</description> 
	'''
	
	def vodml(IncludeDeclaration e) '''
	<include>«e.importURI»</include>
	'''
	
	def vodml(AbstractElement e)
	{
      //shame that xtend does not do dynamic dispatch - at least not in the way that I thought....
      switch  e {
      	PackageDeclaration : (e as PackageDeclaration).vodml
      	ObjectType : (e as ObjectType).vodml
      	Enumeration : (e as Enumeration).vodml
      	DataType : (e as DataType).vodml
      	PrimitiveType: (e as PrimitiveType).vodml
      	default: "unknown type " + e.class
      }
      		
 
	}
	
	def preamble(ReferableElement e) '''
	   <vodml-id>«e.name»</vodml-id>
	   <name>«e.name»</name>
	   <description>«e.description»</description>	    
	'''
	
	def vodml (PackageDeclaration e)'''
	<package>
	   «e.preamble»
      «FOR f: e.elements»
         «f.vodml»
      «ENDFOR»
	</package>
	'''
	def vodml (ObjectType e)'''
	<objectType «IF e.abstract» abstract='true'«ENDIF»>
	   «e.preamble»
	   «IF e.superType != null»
	   <extends>
	      <utype>«e.superType.name»</utype>
	   </extends>
	   «ENDIF»
	   «FOR f: e.constraints»
	   	«f.vodml»
	   «ENDFOR»
	   «FOR f: e.attributes»
	   	«f.vodml»
	   «ENDFOR»
	</objectType>
	'''
	def vodml (Attribute e)'''
	«IF e.composition»
	<composition>
	«ELSE»
	<attribute>
	«ENDIF»
	  «e.preamble»
	  «(e.type as ReferableElement).vodml»
	  «vodml(e.multiplicity)»
	«IF e.composition»
	</composition>
	«ELSE»	
	</attribute>
	«ENDIF»
	'''

   def vodml(ReferableElement e)'''
   <datatype><!--this does not have the proper UType yet -->
     <utype>«e.name»</utype>
   </datatype>
   '''

// this is not really doing correct thing for attributes yet...
   def vodml(Constraint e)'''
   <constraint>
      «IF e.expr != null»
        <expression>«e.expr»</expression>
        <language>«e.language»</language>
      «ENDIF»
   </constraint>
   '''


	def vodml (Enumeration e)'''
	<enumeration>
	   «e.preamble»
      «FOR f: e.literals»
         «f.vodml»
      «ENDFOR»
	<enumeration>
	'''
	def vodml (DataType e)'''
	<dataType  «IF e.abstract» abstract='true'«ENDIF»>
	  «e.preamble»
	   «IF e.superType != null»
	   <extends>
	      <utype>«e.superType.name»</utype>
	   </extends>
	   «ENDIF»
	   «FOR f: e.constraints»
	   	«f.vodml»
	   «ENDFOR»
	   «FOR f: e.attributes»
	   	«f.vodml»
	   «ENDFOR»
	</dataType>
	'''
	
	def vodml (Multiplicity e)
	{
		if (e != null)
		{
			if(e.multiplicitySpec != null)
			{
				switch e.multiplicitySpec {
					case ATLEASTONE: {
						vodml(1,-1)
					}
					case MANY: {
						vodml(0,-1)
					}
					case OPTIONAL: {
						vodml(0,1)
					}
					
				}
			}
			else
			{
				vodml(e.minOccurs, e.maxOccurs)
			}
		}
		else
		{
		vodml(1, 1)
		}
	}
	
	def vodml(int minOccurs, int maxOccurs)'''
	<multiplicity>
	  <minOccurs>«minOccurs»</minOccurs>
	  <maxOccurs>«maxOccurs»</maxOccurs>
	</multiplicity>
	'''
	
	
	def vodml (PrimitiveType e)'''
   <primitiveType>
     «e.preamble»
   </primitiveType>	
	'''
	def vodml (EnumLiteral e)'''
   <literal>
     «e.preamble»
   </literal>	
	'''
	
}
